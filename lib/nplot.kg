:"curve-plotting functions, outputting EPS"
:"try kg -l nstat -l nplot -r 'aplot(cdf;-4;4)' >cdf.eps"

.comment("*****")
frame(X;Y)        draw frame with coordinates X=[x1 xn sx] and Y=[y1 yn sy]
                  where sx,sy = step width
grid(X;Y)         like frame, but with a grid
cframe(X;Y)       like frame, but X contains category names
cgrid(X;Y)        like grid, but X contains category names
frame3(X;Y;Z)     draw 3D frame with coordinates X=[x1 xn sx], Y=[y1 yn sy],
                  and Z=[z1 zn sz], where sx,sy,sz = step width
grid3(X;Y;Z)      like frame3, but with a grid
cframe3(X;Y;Z)    like frame3, but Z contains category names
cgrid3(X;Y;Z)     like grid3, but Z contains category names
plot(fn)          plot function in a given frame/grid
plot3(fn)         plot dyadic function in 3D frame/grid
replot()          re-plot the most recently plotted 2d curve
area3(fn)         like plot3, but plot gradient-colored areas under the curve
area(x0;xn;b)     fill area x0..xn below a plotted curve;
                  b = base line (mostly 0)
vline(x)          plot the value of a fn as a vertical line at x
barplot(data)     plot bar graph from data in frame/grid
bar(x;y;n)        re-plot x'th bar of n-bar graph with height y
scplot(data)      plot scatter graph from data in frame/grid
segplot(data)     segment-plot (run-plot) data in frame/grid
scplot2(data)     scatter-plot pairs [x y]... in frame/grid
errplot(x;fn)     plot error bars for each pair in x given model fn
errbar([x y];fn)  plot an error bar from x/y to x/fn(x)
text(x;y;text)    insert text into diagram; x,y in 0..499
ctext(x;y;text)   insert centered text
rtext(x;y;text)   insert right-aligned text
draw()            finish drawing; not required with aplot
aplot(fn;x0;xn)   draw auto-scaled grid and plot function
color()           use orange for filling areas (DEPRECATED; use fill*)
gray()            use gray for filling areas (DEPRECATED; use fill*)
setfill(x)        set custom fill color (DEPRECATED; use fill*)
setgray(x)        set line color to 1-x percent gray (0=black)
setrgb(r;g;b)     set line color to the given RGB values (0<=x<=1)
fillgray(x)       set fill color to x% gray (0=black, 1=white)
fillrgb(r;g;b)    set fill color to the given RGB values (0<=x<=1)
setcanvas(r;g;b)  set canvas color to given RGB values (0<=x<=1)
                  (must be before grid/frame)
setfont(x)        set font (Postscript font name)
setgrid(x)        set grid density (3=dense, 7=sparse)
setline(x)        set line pattern (0..4)
setwidth(x)       set line width (default: 1)
setres(x)         set X resolution to x (Z%10..Z)
settext(x)        set font size in pts
setdot(x)         set scatter dot size (default: 5)
setbar(x)         set bar width to x*100 percent
setzspace(x)      set z-mark spacing to every x'th mark
setgrad(r;g;b)    set gradient to [r1 rn];[g1 gn];[b1 bn], where x1 is
                  the starting weight and xn is the final weight of each
                  channel; weights range from 0 to 1.
title(x)          print a title above the plot (2D plot)
xtitle(x)         print a title next to the x-axis (2D plot)
ytitle(x)         print a title next to the y-axis (2D plot)
title3(x)         print a title next to the y-axis (3D plot)
xtitle3(x)        print a title next to the x-axis (3D plot)
ytitle3(x)        print a title next to the y-axis (3D plot)
ztitle3(x)        print a title next to the z-axis (3D plot)
key0(x)           key text for line pattern 0 (2D only)
...
key4(x)           key text for line pattern 4
descn(n;x)        n'th line of description text (2D only)
desc0(x)          first line of description text (2D only)
...
desc4(x)          last line of description text

Examples: frame([-4 4 1];[0 16 1])
          plot({x^2})
          area(0;3;0)
          draw()

          settext(20)
          setzspace(4)
          grid3([-1 1 0.2];[-1 1 0.2];[-1 1 0.05])
          plot3({(x^2)-y^2})
          draw()
*****

.l("math.kg")
.l("help.kg")
.module(:nplot)
Fun::0
X0::Xn::Y0::Yn::0
Tx::Ty::0
S::[]
Fm::"1.0 0.75 0 setrgbcolor"
Fo::"Times-Bold"
Fs::14
Ds::5
Bw::1.0
Ls::0
Lw::1
Co::0
Bg::[]
Gt::3
Z::500  :"canvas size"
R::200  :"resolution"
Xoff::50
Yoff::50
Rnd::0
done::{.fl()}
dflstyle::{lstyle(0);cstyle(Co);lwidth(1)}
style::{lstyle(x);cstyle(y);lwidth(z)}
coord::{.d(Xoff+x*Z%R);.d(" ");.d(Yoff+y*Z%R)}
mt::{coord(x;y);.p(" moveto")}
lt::{coord(x;y);.p(" lineto")}
mta::{.d(Xoff+x);.d(" ");.d(Yoff+y);.p(" moveto")}
lta::{.d(Xoff+x);.d(" ");.d(Yoff+y);.p(" lineto")}
mte::{.d(Xoff+Z);.d(" ");.d(Yoff+0);.p(" moveto")}
stroke::{:[x;.p("closepath");[]];.p("stroke")}
framebox::{dflstyle();
           mta(0;0);
           lta(0;Z);
           lta(Z;Z);
           lta(Z;0);
           stroke(1)}
clip::{mta(0;0);
       lta(0;Z);
       lta(Z;Z);
       lta(Z;0);
       .p("clip")}
backgrd::{.p("newpath");
          .p("0 0 moveto");
          .p((Z+2*Xoff)," 0 lineto");
          .p((Z+2*Xoff)," ",(Z+2*Yoff)," lineto");
          .p("0 ",(Z+2*Yoff)," lineto");
          cstyle(Bg);
          .p("closepath fill")}
prolog::{.p("%!PS-Adobe-3.0");
         .d("%%BoundingBox: 0 0 ");.d(Z+2*Xoff);.d(" ");.p(Z+2*Yoff);
         .d("/Z ");.d(Z);.p(" def");
         .d("/Xoff ");.d(Xoff);.p(" def");
         .d("/Yoff ");.d(Yoff);.p(" def");
         :[Bg;backgrd();0];
         .p("newpath");
         .p("0 setlinecap");
         .p("0 setlinejoin");
         .p("2 setlinewidth");
         dflstyle();
         framebox();
         pstext(Fs);
         []}
pp::{.d'x;.p("")}
xmark::{pp("dup stringwidth pop 2 div ",x," exch sub ",Xoff,
           " add ",Yoff," ",Fs," sub moveto")}
ymark::{pp("dup stringwidth pop 46 exch sub ",x," ",Yoff," add moveto")}
xgrid::{pp((x+Xoff)," ",Yoff," moveto");
        pp((x+Xoff)," ",(Z+Yoff)," lineto");
        xmark(x)}
nxgrid::{pp((x+Xoff)," ",Yoff," moveto");
         pp((x+Xoff)," ",(Z+Yoff)," lineto");
         pp(" pop <>")}
ygrid::{pp(Xoff," ",(x+Yoff)," moveto");
        pp((Z+Xoff)," ",(x+Yoff)," lineto");
        ymark(x)}
pstext::{.p("/",Fo," findfont");.d(Fs::x);.p(" scalefont setfont")}
settext::{Fs::x}
setgrid::{Gt::x}
setdot::{Ds::x}
setbar::{Bw::x}
scfn::{x}
scale::{[s sn sx xx nn grid pp];
        s::x@2;xx::Z%(%s)*nn::#(x@1)-*x;grid::y;
        .p("newpath");
        pstext(Fs-2);
        .p("1 setlinecap");
        .p("1 setlinejoin");
        :[Gt;.p("[0 ",($Gt),"] 0 setdash");0];
        .p(($Lw)," setlinewidth");
        sn::*x+0.0;
        sx::0.0;
        (1+_nn%s){x;pp::scfn(:[Rnd;rndn(sn;Rnd);sn]);.d(:[pp~_pp;_pp;pp]);
                  .p(" 20 string cvs");grid(sx);.p("show");
                  sn::sn+s;sx::sx+xx}:*0;
        pstext(Fs+2);
        stroke(0);
        .p("[] 0 setdash")}
ascale::{[r];r::y-x;Rnd:::[r<1;_2+#ln(r)%2;3];scale(x,y,r%10;z)}
cats::{[s];s::Z%#x;(!#x){ctext((s%2)+x*s;-15;y)}'x}
newpath::{.p("newpath")}
fill::{.p(" ",Fm," fill 0 setgray")}
cfill::{.p("closepath");fill()}
setfill::{Fm::x}
setres::{R::x}
lstyle::{:[0=x;.p("[] 0 setdash")
         :|1=x;.p("[5 5] 0 setdash")
         :|2=x;.p("[2 4] 0 setdash")
         :|3=x;.p("[4 3 1 3] 0 setdash")
         ;.p("[1 3 1 3 5 3] 0 setdash")]}
cstyle::{:[@x;.p(($x)," setgray")
          ;.p(($*x)," ",($x@1)," ",($x@2)," setrgbcolor")]}
lwidth::{.p(($x)," setlinewidth")}
setline::{Ls::x}
setwidth::{Lw::x}
setgray::{Co::x}
setrgb::{Co::x,y,z}
setfont::{Fo::x}
setcanvas::{Bg::x,y,z}
fillgray::{setfill(($x)," setgray")}
fillrgb::{setfill(($x)," ",($y)," ",($z)," setrgbcolor")}
gray::{fillgray(0.7)}
color::{fillrgb(1.0;0.75;0)}
epilog::{.p("showpage");.fl()}
run::{[a b f];Fun::f::x;a::R%#z-y;b::y;{f((x%a)+b)}'!R}
aplot::{[r l h ll lh];r::run(x;y;z);h::R%(lh::|/r)-l::ll::&/r;
       prolog();ascale(y;z;xgrid);ascale(l;|/r;ygrid);clip();
       mt(0;0);(!R){lt(x;h*y-l)}'r;mte();stroke(1);epilog();
       Rnd,ll,lh,0.1*#lh-ll}
grid::{prolog();scfn::{x};
       scale((X0::*x),(Xn::x@1),Tx::x@2;xgrid);
       scale((Y0::*y),(Yn::y@1),Ty::y@2;ygrid);done()}
frame::{prolog();scfn::{x};
        scale((X0::*x),(Xn::x@1),Tx::x@2;xmark);
        scale((Y0::*y),(Yn::y@1),Ty::y@2;ymark);done()}
cframe::{prolog();scfn::{x};cats(x);X0::1;Xn::1+#x;
         scale((Y0::*y),(Yn::y@1),Ty::y@2;ymark);done()}
cgrid::{prolog();scfn::{x};cats(x);scale((X0::1),(Xn::1+#x),1;nxgrid);
        scale((Y0::*y),(Yn::y@1),Ty::y@2;ygrid);done()}
lgrid::{prolog();scfn::{x};
        scale((X0::*x),(Xn::x@1),Tx::x@2;xgrid);
        scfn::{_10^x};scale((Y0::*y),(Yn::y@1),Ty::y@2;ygrid);done()}
lframe::{prolog();scfn::{x};
         scale((X0::*x),(Xn::x@1),Tx::x@2;xmark);
         scfn::{_10^x};scale((Y0::*y),(Yn::y@1),Ty::y@2;ymark);done()}
replot::{[r h];h::R%#Yn-Y0;newpath();.p("1 setlinecap 1 setlinejoin");
         style(Ls;Co;Lw);mt(0;0);(!R){lt(x;h*y-Y0)}'S;mte();stroke(1);done()}
plot::{clip();S::run(x;X0;Xn);replot()}
box::{newpath();mt(x;0);lt(x;y);lt(x+z;y);lt(x+z;0)}
fillbox::{box(x;y;z);cfill();box(x;y;z);stroke(1)}
bar::{[w h m];w::R%z;m::w*1-Bw;h::R%#Yn-Y0;fillbox((x*w)+m%2;y*h;w-m)}
barplot::{[s];clip();s::#x;dflstyle();(!s)bar(;;s)'x;framebox()}
arc::{.p(" 0 360 arc closepath ")}
circle::{newpath();coord(x;y);.d(" ");.d(Ds);arc();fill();
         newpath();coord(x;y);.d(" ");.d(Ds);arc();stroke(0)}
scplot::{[w h];clip();w::R%#x-X0;h::R%#Yn-Y0;dflstyle();
         (0.5+(!#x)+X0){circle(w*x-X0;h*y-Y0)}'x;stroke(0)}
segplot::{[w h p];clip();w::R%#x-X0;h::R%#Yn-Y0;p::mt;style(Ls;Co;Lw);
          (0.5+(!#x)+X0){p(w*x-X0;h*y-Y0);p::lt}'x;stroke(0)}
scplot2::{[w];w::R%#Xn-X0;h::R%#Yn-Y0;dflstyle();
          (*'x){circle(w*x-X0;h*y-Y0)}'{x@1}'x;stroke(0)}
vline::{[w h];newpath();w::R%#Xn-X0;h::R%#Yn-Y0;style(Ls;Co;Lw);
        mt(w*x-X0;h*-Y0);lt(w*x-X0;h*Fun(x)-Y0);stroke(0)}
errbar::{[w h xx yy fn];newpath();w::R%#Xn-X0;h::R%#Yn-Y0;xx::*x;yy::x@1;fn::y;
         style(Ls;Co;Lw);mt(w*xx-X0;h*yy-Y0);lt(w*xx-X0;h*fn(xx)-Y0);stroke(0)}
errplot::{[mod];mod::y;errbar(;mod)'x}
run::{[a b f];Fun::f::x;a::R%#z-y;b::y;{f((x%a)+b)}'!R}
area::{[r h u v x0 xn];h::R%#Yn-Y0;u::R%#Xn-X0;v::(R%#Yn-Y0)*z-Y0;
       x0::u*x-X0;xn::u*y-X0;
       newpath();mt(x0;v);
       (!R){:[~(x<x0)|x>xn;lt(x;h*y-Y0);[]]}'S;lt(xn;v);cfill();
       replot();framebox()}
xc::"0123456789ABCDEF"
hextext::{,/{(xc@_(#x)%16),xc@_(#x)!16}'x}
text::{pstext(Fs);mta(x;y);.d("<",hextext(z),">");.p("show")}
ctext::{pstext(Fs);mta(x;y);.d("<",hextext(z),">");
        .p(" dup stringwidth pop 2 div neg 0 rmoveto show")}
rtext::{pstext(Fs);mta(x;y);.d("<",hextext(z),">");
        .p(" dup stringwidth pop neg 0 rmoveto show")}
title::{[s];pstext(_1.5*s::Fs);ctext(Z%2;Z+Fs%2;x);pstext(s)}
lkey::{mta(Z-45;(Z-5+1*Fs)-(Fs*x)-Fs%3);lstyle(x);.p("35 0 rlineto stroke")}
key0::{rtext(Z-50;Z-5+1*Fs;x);lkey(0)}
key1::{rtext(Z-50;Z-5+2*Fs;x);lkey(1)}
key2::{rtext(Z-50;Z-5+3*Fs;x);lkey(2)}
key3::{rtext(Z-50;Z-5+4*Fs;x);lkey(3)}
key4::{rtext(Z-50;Z-5+5*Fs;x);lkey(4)}
descn::{rtext(Z-15;Z-5+(1+x)*Fs;y)}
desc0::descn(0;)
desc1::descn(1;)
desc2::descn(2;)
desc3::descn(3;)
desc4::descn(4;)
xtitle::{ctext(Z%2;-35;x)}
ytitle::{mta(Z+15;Z%2);.d("<",hextext(x),">");
         .d(" dup stringwidth pop 2 div 0 exch rmoveto ");
         .p("gsave 270 rotate show grestore")}
       
draw::epilog

:"***** 3D Plotter *****"

Zoff::Z%2
Zmin::1
Zmax::5
Zstep::1
Zsp::1
Zmarks::[]
Sp::1
Zp::0
Za::0
Gr::[[1 1] [0 1] [0 0]]
coord3::{.d(Za+Xoff+x*Z%R);.d(" ");.d(Za+Yoff+y*Z%R)}
mt3::{coord3(x;y);.p(" moveto")}
lt3::{coord3(x;y);.p(" lineto")}
mte3::{.d(Xoff+Za+Z+Z%2);.d(" ");.d(Yoff+Za+0);.p(" moveto")}
lte3::{.d(Xoff+Za+Z);.d(" ");.d(Yoff+Za+0);.p(" lineto")}
framebox3::{dflstyle();
            mta(0;0);
            lta(Z%2;Z%2);
            lta(Z+Z%2;Z%2);
            lta(Z;0);
            stroke(1);
            mta(0;0);
            lta(0;Z);
            lta(Z%2;Z+Z%2);
            lta(Z%2;Z%2);
            stroke(0);
            mta(Z%2;Z+Z%2);
            lta(Z+Z%2;Z+Z%2);
            lta(Z+Z%2;Z%2);
            stroke(0)}
backgrd3::{.p("newpath");
           .p("0 0 moveto");
           .p((_(1.5*Z)+2*Xoff)," 0 lineto");
           .p((_(1.5*Z)+2*Xoff)," ",(_(1.5*Z)+3*Yoff)," lineto");
           .p("0 ",(_(1.5*Z)+3*Yoff)," lineto");
           cstyle(Bg);
           .p("closepath fill")}
prolog3::{.p("%!PS-Adobe-3.0");
          .d("%%BoundingBox: 0 0 ");.d(_(1.5*Z)+2*Xoff);.d(" ");
          .p(_(1.5*Z)+3*Yoff);
          .d("/Z ");.d(Z);.p(" def");
          .d("/Xoff ");.d(Xoff);.p(" def");
          .d("/Yoff ");.d(Yoff);.p(" def");
          .d("/Zoff ");.d(Zoff);.p(" def");
          :[Bg;backgrd3();0];
          .p("newpath");
          .p("0 setlinecap");
          .p("0 setlinejoin");
          .p("2 setlinewidth");
          dflstyle();
          framebox3();
          []}
setzspace::{Zsp::x}
setgrad::{Gr::(,x),(,y),,z}
zmark::{pp("dup stringwidth pop 46 exch sub ",x," 2 div add ",x," 2 div ",
        Yoff," add Z add 15 add moveto")}
xgrid3::{pp((x+Xoff+Zoff)," ",(Yoff+Zoff)," moveto");
         pp((x+Xoff+Zoff)," ",(Z+Yoff+Zoff)," lineto");
         pp((x+Xoff)," ",Yoff," moveto");
         pp((x+Xoff+Z%2)," ",(Yoff+Z%2), " lineto");
         xmark(x)}
ygrid3::{pp((Xoff+Zoff)," ",(x+Yoff+Zoff)," moveto");
         pp((Z+Xoff+Zoff)," ",(x+Yoff+Zoff)," lineto");
         pp(Xoff," ",(x+Yoff)," moveto");
         pp((Xoff+Zoff)," ",(x+Yoff+Zoff)," lineto");
         ymark(x)}
zgrid3::{pp((Xoff+x%2)," ",(Yoff+x%2)," moveto");
         pp((Xoff+x%2)," ",(Z+Yoff+x%2)," lineto");
         pp((Xoff+x%2)," ",(Yoff+x%2)," moveto");
         pp((Z+Xoff+x%2)," ",(Yoff+x%2)," lineto");
         zmark(x)}
scale3::{[fs fm s sx sn xx nn grid];s::x@2;xx::Z%(%s)*nn::#(x@1)-*x;grid::y;
         fs::{x;.d(sn);.p(" 20 string cvs");grid(sx);.p("show");
              sn::sn+s*Sp;sx::sx+xx*Sp};
         fm::{.d("<",hextext(Zmarks@x),"> ");grid(sx);.p("show");
              sn::sn+s*Sp;sx::sx+xx*Sp;x+1};
         .p("newpath");
         pstext(Fs-2);
         .p("1 setlinecap");
         .p("1 setlinejoin");
         :[Gt;.p("[0 ",($Gt),"] 0 setdash");0];
         .p(($Lw)," setlinewidth");
         sn::0.0+*x;
         sx::0.0;
         (1+_(nn%s)%Sp){:[Zmarks;fm(x);fs(x)]}:*0;
         pstext(Fs+2);
         stroke(0);
         .p("[] 0 setdash")}
run3::{[f a b];f::x;a::R%#z-y;b::y;{f((x%a)+b;Zp)}'!R}
grid3::{prolog3();Sp::1;scale3((X0::*x),(Xn::x@1),Tx::x@2;xgrid3);
                 Sp::1;scale3((Y0::*y),(Yn::y@1),Ty::y@2;ygrid3);
                 Sp::Zsp;scale3((Zmin::*z),(Zmax::z@1),Zstep::z@2;zgrid3);
                 done()}
frame3::{prolog3();Sp::1;scale3((X0::*x),(Xn::x@1),Tx::x@2;xmark);
                  Sp::1;scale3((Y0::*y),(Yn::y@1),Ty::y@2;ymark);
                  Sp::Zsp;scale3((Zmin::*z),(Zmax::z@1),Zstep::z@2;zmark);
                  done()}
cgrid3::{prolog3();Sp::1;scale3((X0::*x),(Xn::x@1),Tx::x@2;xgrid3);
                  Sp::1;scale3((Y0::*y),(Yn::y@1),Ty::y@2;ygrid3);
                  Zmarks::z;
                  Sp::Zsp;scale3((Zmin::0),(Zmax::(#z)-1),Zstep::1;zgrid3);
                  done()}
cframe3::{prolog3();Sp::1;scale3((X0::*x),(Xn::x@1),Tx::x@2;xmark);
                   Sp::1;scale3((Y0::*y),(Yn::y@1),Ty::y@2;ymark);
                   Zmarks::z;
                   Sp::Zsp;scale3((Zmin::0),(Zmax::(#z)-1),Zstep::1;zmark);
                   done()}
replot3::{[r h];h::R%#Yn-Y0;newpath();.p("1 setlinecap 1 setlinejoin");
          mt3(0;h*(*S)-Y0);(!R){lt3(x;h*y-Y0)}'S;:[x;lte3();mte3()];
          :[x;lt3(0;0);0];stroke(1);done()}
plot3::{[fn adj];Zp::Zmin;Za::0;fn::x;adj::(Z%2)%(Zmax-Zmin)%Zstep;
        {~x>Zmax}{Zp::x;S::run3(fn;X0;Xn);replot3(0);Za::Za+adj;x+Zstep}:~Zmin}
gcolor::{[r g b];r::Gr@0;g::Gr@1;b::Gr@2;
         r:::[(*r)=r@1;*r:|(*r)<r@1;(*r)+x*(r@1)-*r;(*r)-x*(*r)-r@1];
         g:::[(*g)=g@1;*g:|(*g)<g@1;(*g)+x*(g@1)-*g;(*g)-x*(*g)-g@1];
         b:::[(*b)=b@1;*b:|(*b)<b@1;(*b)+x*(b@1)-*b;(*b)-x*(*b)-b@1];
         .p(($r)," ",($g)," ",($b)," setrgbcolor")}
gfill::{gcolor(1-Za%Z%2);.p("closepath fill 0 setgray")}
gradrp3::{[r h];h::R%#Yn-Y0;newpath();.p("1 setlinecap 1 setlinejoin");
          mt3(0;0);(!R){lt3(x;h*y-Y0)}'S;lte3();gfill();done()}
area3::{[fn adj];Zp::Zmax;Za::Z%2;fn::x;adj::(Z%2)%(Zmax-Zmin)%Zstep;
        {~x<Zmin}{Zp::x;S::run3(fn;X0;Xn);gradrp3();replot3(1);
        Za::Za-adj;x-Zstep}:~Zmax}
title3::{[s];pstext(_1.5*s::Fs);ctext(Z;(Z%2)+Z+Fs%2;x);pstext(s)}
xtitle3::xtitle
ytitle3::{mta((Z%2)+Z+15;Z);.d("<",hextext(x),">");
         .d(" dup stringwidth pop 2 div 0 exch rmoveto ");
         .p("gsave 270 rotate show grestore")}
ztitle3::{mta((Z%4)+Z+30;Z%4);.d("<",hextext(x),">");
         .d(" dup stringwidth pop 2 div neg dup rmoveto ");
         .p("gsave 45 rotate show grestore")}

help.add([
 ["aplot(fn;x1;xn)" "draw auto-scaled grid and plot function"]
 ["area(x1;xn;b)" "fill area x1..xn below a plotted curve; b = base line"]
 ["area3(fn)" "plot dyadic function fn in 3D frame/grid using color gradients"]
 ["bar(x;y;n)" "re-plot x'th bar of n-bar graph with height y"]
 ["barplot(D)" "plot bar graph from data set D in given frame/grid"]
 ["cframe(X;Y)" "draw category frame with categories X and coordinates Y"]
 ["cframe3(X;Y;Z)" "draw 3D catecory frame with coords X/Y and categories Z"]
 ["cgrid(X;Y)" "draw category grid with categories X and coordinates Y"]
 ["cgrid3(X;Y;Z)" "draw 3D catecory frame with coords X/Y and categories Z"]
 ["ctext(x;y;text)" "insert centered text into diagram; x,y in 0..499"]
 ["desc0(x)" "1st line of description text (2D only)"]
 ["desc1(x)" "2nd line of description text (2D only)"]
 ["desc2(x)" "3rd line of description text (2D only)"]
 ["desc3(x)" "4th line of description text (2D only)"]
 ["desc5(x)" "5th line of description text (2D only)"]
 ["descn(n;x)" "n'th line of description text (2D only)"]
 ["draw()" "finish drawing; not required with aplot"]
 ["errbar([x y];fn)" "plot an error bar from x/y to x/fn(x)"]
 ["errplot(XY;fn)" "plot error bars for each pair in XY given model fn"]
 ["fillgray(x)" "set fill color to x percent gray (0=black, 1=white)"]
 ["fillrgb(r;g;b)" "set fill color to the given RGB values (0<=x<=1)"]
 ["frame(X;Y)" "draw frame with coordinates X/Y=[start end step]"]
 ["frame3(X;Y;Z)" "draw 3D frame with coordinates X/Y/Z=[start end step]"]
 ["grid(X;Y)" "draw grid with coordinates X/Y=[start end step]"]
 ["grid3(X;Y;Z)" "draw 3D frame with coordinates X/Y/Z=[start end step]"]
 ["key0(x)" "key text for line pattern 0 (2D only)"]
 ["key1(x)" "key text for line pattern 1 (2D only)"]
 ["key2(x)" "key text for line pattern 2 (2D only)"]
 ["key3(x)" "key text for line pattern 3 (2D only)"]
 ["key4(x)" "key text for line pattern 4 (2D only)"]
 ["plot(fn)" "plot function fn in a given frame/grid"]
 ["plot3(fn)" "plot dyadic function fn in 3D frame/grid"]
 ["replot()" "re-plot the most recently plotted 2d curve"]
 ["rtext(x;y;text)" "insert right-adjusted text into diagram; x,y in 0..499"]
 ["scplot(D)" "plot scatter diagram from data set D in given frame/grid"]
 ["scplot2(XY)" "scatter-plot pairs [x y] of set XY in given frame/grid"]
 ["segplot(D)" "segment-plot (run-plot) data set D in given frame/grid"]
 ["setdot(x)" "set scatter dot size (default: 5)"]
 ["setbar(x)" "set bar width to x*100 percent"]
 ["setfont(x)" "set font (Postscript font name)"]
 ["setgrad(r;g;b)" "set gradients for area3 plot to [r1 rn];[g1 gn];[b1 bn]"]
 ["setgray(x)" "set line color to 1-x percent gray (0=black)"]
 ["setgrid(x)" "set grid density (3=dense, 7=sparse)"]
 ["setline(x)" "set line pattern (0..4)"]
 ["setwidth(x)" "set line width (default: 1)"] 
 ["setres(x)" "set X resolution to x (50..500)"]
 ["setrgb(r;g;b)" "set line color the given RGB values (0<=x<=1)"]
 ["settext(x)" "set font size in pts"]
 ["setzspace(x)" "set z-mark spacing to every x'th mark"]
 ["text(x;y;text)" "insert text into diagram; x,y in 0..499"]
 ["title(x)" "print a title above the plot (2D plot)"]
 ["title3(x)" "print a title next to the y-axis (3D plot)"]
 ["vline(x)" "plot value of recently plotted fn as a vertical line at x"]
 ["xtitle(x)" "print a title next to the x-axis (2D plot)"]
 ["xtitle3(x)" "print a title next to the x-axis (3D plot)"]
 ["ytitle(x)" "print a title next to the y-axis (2D plot)"]
 ["ytitle3(x)" "print a title next to the y-axis (3D plot)"]
 ["ztitle3(x)" "print a title next to the z-axis (3D plot)"]
])

.module(0)
